apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'dev.simurgh'
version = '0.1.6'



task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

task macNativeJar(type: Jar, dependsOn: ":desktop:buildMac") {
    archiveClassifier.set('mac')
    manifest {
        attributes('Manifest-Version': version)
    }
    from('../build/') {
        include 'libs/mac/libnanovg.dylib'
    }
}

task win64NativeJar(type: Jar, dependsOn: ":desktop:buildWin64") {
    archiveClassifier.set('win64')
    manifest {
        attributes('Manifest-Version': version)
    }
    from('../build/') {
        include 'libs/win64/nanovg.dll'
    }
}

task iosNativeJar(type: Jar, dependsOn: ":ios:buildIPhone") {
    archiveClassifier.set('ios')
    manifest {
        attributes('Manifest-Version': version)
    }
    from('../build/libs/iphone/') {
        include 'META-INF/robovm/ios/libs/libnanovg.a'
    }
    from('../ios/') {
        include 'META-INF/robovm/ios/robovm.xml'
    }
}


artifacts {
    archives jar
    archives macNativeJar
    archives win64NativeJar
    archives file: file('../android/app/build/outputs/aar/app-release.aar'), name: 'nanovg', type: 'aar', classifier: 'android'
    archives iosNativeJar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name "Nanovg native library"
                description 'Nanovg dynamic library and jni code for use in simurgh'
                artifactId "nanovg"
                url 'https://github.com/sadeghpro/simurgh_nanovg'
                scm {
                    url 'git@github.com:sadeghpro/simurgh_nanovg.git'
                    connection 'git@github.com:sadeghpro/simurgh_nanovg.git'
                    developerConnection 'git@github.com:sadeghpro/simurgh_nanovg.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'sadeghpro'
                        name 'sadegh mohammadnia'
                    }
                }
            }
        }
    }
}

uploadArchives.dependsOn(':android:app:build')